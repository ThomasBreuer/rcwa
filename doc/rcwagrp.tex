%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%W  rcwagrp.tex              RCWA documentation               Stefan Kohl
%%
%H  @(#)$Id$
%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

\Chapter{Residue Class-Wise Affine Groups}

This chapter describes the functionality available for calculating with
integral rcwa groups.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{The category of integral rcwa groups}

\>IsIntegralRcwaGroup( <G> ) C
\>IsRcwaGroup( <G> ) C

The category of all integral rcwa groups.

The version `IsRcwaGroup' is reserved as a term for denoting any kind
of rcwa groups, if in future versions also rcwa groups over other
PID's will be implemented.

\>`IntegralRcwaGroupsFamily' V

The family of all integral rcwa groups.

All integral rcwa groups are subgroups of

\>RCWA( Integers ) F

The group RCWA($\Z$) of all bijective integral rcwa mappings.
This group is not finitely generated, hence no generators are stored.

\beginexample
gap> RCWA_Z := RCWA(Integers);
RCWA(Z)
gap> Size(RCWA_Z);
infinity
gap> IsFinitelyGeneratedGroup(RCWA_Z);
false
gap> One(RCWA_Z);
<identity integral rcwa mapping>
gap> IsSubgroup(RCWA_Z, Group(RcwaMapping([[-1,0,1]]),
>                             RcwaMapping((1,2,3),[1..4]),
>                             RcwaMapping(2,[[0,1],[1,0],[2,3],[3,2]])));
true 
\endexample

All integral rcwa groups are supergroups of

\>`TrivialIntegralRcwaGroup' V

The trivial integral rcwa group.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Constructing rcwa groups}

Rcwa groups can be constructed using either `Group', `GroupByGenerators'
or `GroupWithGenerators', as usual (see reference manual).
Note that currently only finitely generated rcwa groups are supported.

\beginexample
gap> g := RcwaMapping([[1,0,1],[1,1,1],[3,6,1],
>                      [1,0,3],[1,1,1],[3,6,1],
>                      [1,0,1],[1,1,1],[3,-21,1]]);;
gap> h := RcwaMapping([[1,0,1],[1,1,1],[3,6,1],
>                      [1,0,3],[1,1,1],[3,-21,1],
>                      [1,0,1],[1,1,1],[3,6,1]]);;
gap> Order(g);
9
gap> Order(h);
9
gap> G := Group(g,h);
<integral rcwa group with 2 generators>
gap> Size(G);
infinity
\endexample

Another possible way to get an rcwa group is by ``translating'' a
permutation group, or by taking the image of an rcwa representation.

\>IntegralRcwaGroupByPermGroup( <G> ) F
\>RcwaGroupByPermGroup( <G> ) F

Constructs an integral rcwa group isomorphic to the (finite) permutation
group <G>, which acts on the range `[ 1 .. LargestMovedPoint( <G> ) ]'
as <G> does.

\beginexample
gap> H := RcwaGroupByPermGroup(Group((1,2),(3,4),(5,6),(7,8),
>                                    (1,3)(2,4),(1,3,5,7)(2,4,6,8)));
<integral rcwa group with 6 generators>
gap> Size(H);
384
gap> IsSolvable(H);
true
gap> List(DerivedSeries(H),Size);
[ 384, 96, 32, 2, 1 ]
\endexample

\>IsomorphismIntegralRcwaGroup( <G> ) A
\>IsomorphismRcwaGroup( <G> ) A

A faithful integral rcwa representation of the group <G>.
Currently only supported for finite groups.

\beginexample
gap> G := GL(2,5);
GL(2,5)
gap> IsomorphismRcwaGroup(G);
CompositionMapping(
[ (1,2,4,8)(3,6,12,18)(5,10,17,22)(9,15,21,24)(13,14,19,23),
  (1,3,7)(2,5,11)(4,9,16)(6,13,12)(8,14,20)(10,18,17)(15,22,21)(19,24,23)
 ] -> [ <integral rcwa mapping with modulus 24>,
  <integral rcwa mapping with modulus 24> ], <action isomorphism> )             
\endexample

\>Display( <G> ) M

Displays the rcwa group <G>.

\beginexample
gap> a := RcwaMapping([[3,0,2],[3, 1,4],[3,0,2],[3,-1,4]]);;
gap> b := RcwaMapping([[3,0,2],[3,13,4],[3,0,2],[3,-1,4]]);;
gap> c := RcwaMapping([[3,0,2],[3, 1,4],[3,0,2],[3,11,4]]);;
gap> H := Group(a,b);
<integral rcwa group with 2 generators>
gap> Display(H);

Rcwa group, generated by

[

Bijective integral rcwa mapping with modulus 4, of order infinity

               n mod 4                  ||              f(n)              
----------------------------------------+---------------------------------
  0 2                                   || 3n/2
  1                                     || (3n + 1)/4
  3                                     || (3n - 1)/4


Bijective integral rcwa mapping with modulus 4

               n mod 4                  ||              f(n)              
----------------------------------------+---------------------------------
  0 2                                   || 3n/2
  1                                     || (3n + 13)/4
  3                                     || (3n - 1)/4

]

\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Computing with rcwa groups}

\>`<g> in <G>' {rcwa groups!element testing}

Tries to figure out whether <g> is an element of <G> or not.
In case <G> is finite, the result is correct provided that the result
given by `IsomorphismPermGroup( G )' is.
If <G> is infinite and <g> is not in <G>, this method may run into an
infinite loop.

\beginexample
gap> u := RcwaMapping([[3,0,5],[9,1,5],[3,-1,5],[9,-2,5],[9,4,5]]);; 
gap> u in H;
false
\endexample

\>Size( <G> ) M

Tries to compute the order of the rcwa group <G>.

This is a probabilistic method.
It may return the size of a proper factor group of <G>, or run in an
infinite loop. You can increase the option value <Steps> to decrease the
probability of getting a wrong result. The default value for <Steps>
is 10.

\beginexample
gap> g1 := RcwaMapping((1,2),[1..2]);
<integral rcwa mapping with modulus 2>
gap> g2 := RcwaMapping((1,2,3),[1..3]);
<integral rcwa mapping with modulus 3>
gap> g3 := RcwaMapping((1,2,3,4,5),[1..5]);
<integral rcwa mapping with modulus 5>
gap> G := Group(g1,g2,g3);
<integral rcwa group with 3 generators>
gap> Size(G);
265252859812191058636308480000000
\endexample

\>IsomorphismPermGroup( <G> ) M

Tries to compute an isomorphism from a finite rcwa group <G> to some
permutation group.

This is a probabilistic method, also.
It may return a homomorphism to a permutation group isomorphic to a
proper factor group of <G>. You can increase the option value <Steps> to
decrease the probability of getting a wrong result. The default value
for <Steps> is 10.

\beginexample
gap> H := Group(g1,g2);
<integral rcwa group with 2 generators>
gap> phi := IsomorphismPermGroup(H);
[ <bijective integral rcwa mapping with modulus 2, of order 2>,
  <bijective integral rcwa mapping with modulus 3, of order 3> ] ->
[ (1,2)(3,4)(5,6), (1,2,3)(4,5,6) ]                                             
\endexample

\>NiceMonomorphism( <G> ) M

Returns the result of `IsomorphismPermGroup' in case the group is
supposed to be finite by `Size', and `IdentityMapping( <G> )' otherwise.

\>NiceObject( <G> ) M

Returns the image of `NiceMonomorphism( <G> )'.

\beginexample
gap> NiceObject(G);
Group([ (1,2)(3,4)(5,6)(7,8)(9,10)(11,12)(13,14)(15,16)(17,18)(19,20)(21,
    22)(23,24)(25,26)(27,28)(29,30), (1,2,3)(4,5,6)(7,8,9)(10,11,12)(13,14,
    15)(16,17,18)(19,20,21)(22,23,24)(25,26,27)(28,29,30),
  (1,2,3,4,5)(6,7,8,9,10)(11,12,13,14,15)(16,17,18,19,20)(21,22,23,24,25)(26,
    27,28,29,30) ])                                                             
\endexample

\>Modulus( <G> ) M

Computes the modulus of the rcwa group <G>.

See also `Modulus' for rcwa mappings, and `IsTame'.

\beginexample
gap> Modulus(G);
30
gap> Modulus(Group(a,b));
0
\endexample

\>PrimeSet( <G> ) O

Computes the prime set of the rcwa group <G>.

See also `PrimeSet' for rcwa mappings.

\beginexample
gap> PrimeSet(G);
[ 2, 3, 5 ]
gap> PrimeSet(H);
[ 2, 3 ]
\endexample

\>IsFlat( <G> ) P

Determines whether or not the rcwa group <G> is flat.

See also `IsFlat' for rcwa mappings.

\beginexample
gap> IsFlat(AlternatingGroup(IsRcwaGroup,5));
true
gap> IsFlat(Group(a,b));
false
gap> IsFlat(Group(g));
false
\endexample

\>IsClassWiseOrderPreserving( <G> ) P

Indicates whether the integral rcwa group <G> is class-wise
order-preserving or not.

See also `IsClassWiseOrderPreserving' for rcwa mappings.

\beginexample
gap> IsClassWiseOrderPreserving(Group(a,b));
true
gap> IsClassWiseOrderPreserving(G);
true
gap> t := RcwaMapping([[-1,0,1]]);;
gap> IsClassWiseOrderPreserving(Group(t,g,h));
false
\endexample

\>IsTame( <G> ) P

Determines whether or not the rcwa group <G> is tame.

See also `IsTame' for rcwa mappings.

\beginexample
gap> IsTame(G);
true
gap> IsTame(Group(a,b));
false
gap> IsTame(Group(Comm(a,b),Comm(a,c)));
true
\endexample

\>ShortOrbits( <G>, <S>, <maxlng> ) F

Computes all finite orbits of the rcwa group <G> of maximal length
<maxlng>, which intersect non-trivially with the set <S>.

\beginexample
gap> A5 := AlternatingGroup(IsRcwaGroup,5);;
gap> ShortOrbits(A5,[-10..10],100);
[ [ -14, -13, -12, -11, -10 ], [ -9, -8, -7, -6, -5 ], [ -4, -3, -2, -1, 0 ], 
  [ 1, 2, 3, 4, 5 ], [ 6, 7, 8, 9, 10 ] ]
gap> Action(A5,last[2]);
Group([ (1,2,3,4,5), (3,4,5) ])
gap> ab := Comm(a,b);; ac := Comm(a,c);;
gap> G := Group(ab,ac);;
gap> orb := ShortOrbits(G,[-20..20],100);
[ [ -51, -48, -42, -39, -25, -23, -22, -20, -19, -17 ], [ -18 ], 
  [ -33, -30, -24, -21, -16, -14, -13, -11, -10, -8 ], 
  [ -15, -12, -7, -6, -5, -4, -3, -2, -1, 1 ], [ -9 ], [ 0 ], 
  [ 2, 3, 4, 5, 6, 7, 8, 10, 12, 15 ], [ 9 ], 
  [ 11, 13, 14, 16, 17, 19, 21, 24, 30, 33 ], [ 18 ], 
  [ 20, 22, 23, 25, 26, 28, 39, 42, 48, 51 ] ]
gap> Action(G,orb[1]);
Group([ (2,6,8,10,4,7), (1,5,7,9,3,6) ])
gap> ShortOrbits(Group(u),[-30..30],100);
[ [ -13, -8, -7, -5, -4, -3, -2 ], [ -10, -6 ], [ -1 ], [ 0 ], [ 1, 2 ], 
  [ 3, 5 ], [ 24, 36, 39, 40, 44, 48, 60, 65, 67, 71, 80, 86, 93, 100, 112, 
      128, 138, 155, 167, 187, 230, 248, 312, 446, 520, 803, 867, 1445 ] ]
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Properties of RCWA(Z)}

\>NrConjugacyClassesOfRCWAZOfOrder( <ord> ) F

Computes the number of conjugacy classes of the whole group RCWA(Z) of
elements of order <ord>.

\beginexample
gap> NrConjugacyClassesOfRCWAZOfOrder(2);
infinity
gap> NrConjugacyClassesOfRCWAZOfOrder(105);
218
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
\Section{Predefined rcwa groups}

There are methods for constructing various types of groups as rcwa
groups. They are just using the ad hoc-translation provided by 
`IntegralRcwaGroupByPermGroup', for the matrix groups after turning to
the image under `IsomorphismPermGroup'. So far, the provided methods
cover all groups listed in the sections concerning basic groups and
classical groups in the chapter describing the group libraries in the
reference manual.

\beginexample
gap> C2 := CyclicGroup(IsRcwaGroup,2);
<integral rcwa group with 1 generator>
gap> G := ExtraspecialGroup(IsRcwaGroup,27,3);
<integral rcwa group with 3 generators>
gap> IsAbelian(G);
false
gap> Exponent(G);
3
gap> S4 := SymmetricGroup(IsRcwaGroup,4);
<integral rcwa group with 2 generators>
gap> Size(S4);
24
gap> G := SylowSubgroup(S4,2);
<integral rcwa group with 3 generators, of size 8>
gap> IdGroup(G);
[ 8, 3 ]
gap> A5 := AlternatingGroup(IsRcwaGroup,5);
<integral rcwa group with 2 generators>
gap> Size(A5);
60
gap> IsSimple(A5);
true
gap> G := GL(IsRcwaGroup,2,3);
<integral rcwa group with 2 generators>
gap> Size(G);
48
\endexample

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%
%E  rcwagrp.tex . . . . . . . . . . . . . . . . . . . . . . . . ends here
